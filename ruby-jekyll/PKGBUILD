# Maintainer: Jimmy Mabey <jimmymabey@gmail.com>
# Contributor: oliparcol <oliparcol AT gmail DOT com>
pkgname=ruby-jekyll
pkgver=1.1.2
pkgrel=1
pkgdesc="A simple, blog aware, static site generator."
arch=('any')
url="http://jekyllrb.com"
license=('MIT')
depends=('ruby-classifier>=1.3' 'ruby-classifier<1.4'
         'ruby-colorator>=0.1' 'ruby-colorator<0.2'
         'ruby-commander>=4.1.3' 'ruby-commander<4.2'
         'ruby-directory_watcher-1.4'
         'ruby-kramdown>=1.0.2' 'ruby-kramdown<1.1'
         'ruby-liquid>=2.5.1'
         'ruby-maruku-0.5'
         'ruby-pygments.rb>=0.5' 'ruby-pygments.rb<0.6'
         'ruby-redcarpet-2.2'
         'ruby-safe_yaml-0.7')
makedepends=('rubygems')
options=(!emptydirs)
changelog=$pkgname.changelog
source=(http://rubygems.org/downloads/jekyll-$pkgver.gem)
noextract=(jekyll-$pkgver.gem)
md5sums=('7e9b850874fa63e42bc2a87f19019adb')

build() {
  cd "$srcdir"

  local gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
  gem install --no-user-install --ignore-dependencies -i "$srcdir$gemdir" -n "$srcdir/usr/bin" \
      "jekyll-$pkgver.gem"
}


package () {
  cd "$srcdir"

  cp -R usr "$pkgdir"
  install -D -m644 $(find -name LICENSE) "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
