# Maintainer: Jimmy Mabey <jimmymabey@gmail.com>
# Contributor: Vadym Abramchuk <abramm@gmail.com>
# Contributor: John Gerritse <reaphsharc@gmail.com>
pkgname=gearman
pkgver=1.1.5
pkgrel=1
pkgdesc="A generic application framework to farm out work to other machines or processes that are better suited to do the work."
arch=('i686' 'x86_64')
url="http://gearman.org/"
license=('BSD')
depends=('boost>=1.39.0' 'libevent')
makedepends=('make')
optdepends=('libdrizzle: Drizzle support'
            'libmemcached: memcached support'
            'postgresql-libs: PostgreSQL support'
            'sqlite: SQLite support'
            'tokyocabinet: Tokyo Cabinet support')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://launchpad.net/${pkgname}d/1.2/$pkgver/+download/${pkgname}d-$pkgver.tar.gz
        ${pkgname}d.service
        ${pkgname}d.conf)
md5sums=('f75a7fa2450ab2fd1dca727b2138cf71'
         'ccc3dc8edfb0c7544c735c3612a941e6'
         '91d27322c963e37be3ce17b80871d322')

build() {
  cd "$srcdir/${pkgname}d-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/${pkgname}d-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm 644 $srcdir/gearmand.conf $pkgdir/etc/gearmand.conf
  install -Dm 644 $srcdir/${pkgname}d-$pkgver/COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  install -Dm 644 $srcdir/gearmand.service $pkgdir/usr/lib/systemd/system/gearmand.service
  install -dm 755 $pkgdir/var/log/gearmand
}
